import kotlin.KotlinVersion

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url = uri("https://dl.bintray.com/korlibs/korlibs/") }
        maven { url = uri("https://plugins.gradle.org/m2/") }
        //maven { url = uri("https://dl.bintray.com/kotlin/kotlin-dev") }
    }

    dependencies {
        classpath "com.soywiz.korlibs:easy-kotlin-mpp-gradle-plugin:0.2.11" // Kotlin 1.3.50: https://github.com/korlibs/easy-kotlin-mpp-gradle-plugin
    }
}

def buildExtraGradleFile = new File(rootDir, "build.extra.gradle")
if (buildExtraGradleFile.exists()) {
	apply from: buildExtraGradleFile
}

subprojects {
	task npmExtract(type: Copy) {
		afterEvaluate {
			dependsOn(jsJar)
			from(zipTree(jsJar.outputs.files.toList().first())) {
				filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: version, KOTLIN_VERSION: "1.3.50"], beginToken: "%%%", endToken: "!!!")
				//filter { line -> line.replaceAll('${version}', version) }
			}
			from new File(rootDir, "README.md")
			into new File(buildDir, "npmPackage")
		}
	}
	task npmPublish(type: Exec, dependsOn: [npmExtract]) {
		workingDir "${buildDir}/npmPackage"
		commandLine 'npm', 'publish', '--access', 'public'
	}
}
